
USE Unilever_000_DBM

--ATRIBUI O PAIS
DECLARE @PAIS VARCHAR(2) = 'MX'

--TRATA O ARQUIVO
IF OBJECT_ID('tempdb..#OPERADOR') <> 0
DROP TABLE #OPERADOR

;WITH CTE AS
(
	SELECT *, ISDATE([Atualizado em]) AS FG_DATA
	  FROM Unilever_000_DBM..[TEMPLATE_TABLE_NAME]
)
SELECT AdAgency_ADDB.dbo.FC_HASH_MD5(LOWER([Endereço de e-mail])) AS HASH_CLIENTE,
	   LOWER([Endereço de e-mail]) AS EMAIL,
	   AdAgency_ADDB.dbo.PrimeiraMaiuscula(Nome) AS PRIMEIRO_NOME,
	   AdAgency_ADDB.dbo.VALIDA_NOME(Nome) AS QA_NOME,
	   IIF(FG_DATA = 0, NULL, CAST([Atualizado em] AS DATETIME)) AS DT_ATUALIZACAO
  INTO #OPERADOR
  FROM CTE

--VALIDA NOME
UPDATE #OPERADOR
   SET QA_NOME = 'INVALIDO'
 WHERE (PRIMEIRO_NOME LIKE '%[^A-Z .]%' AND QA_NOME = 'VALIDO')
	OR  PRIMEIRO_NOME = 'NAN'

--DEDUPLICA EMAILS
;WITH CTE AS
(
	SELECT *, ROW_NUMBER() OVER (PARTITION BY EMAIL ORDER BY DT_ATUALIZACAO DESC) AS DEDUP
	  FROM #OPERADOR
)
DELETE CTE WHERE DEDUP > 1

--CARGA CRM FOOTER
INSERT INTO [AMAZON_176].[unilever_crm].[dbo].[TB_PSQ_CLIENTE] 
(
	PRIMEIRO_NOME,
	[HASH],
	EMAIL,
	ATIVO,
	PAIS,
	DT_CRIACAO
)
SELECT IIF(ISNULL(PRIMEIRO_NOME,'') = '' OR QA_NOME = 'INVALIDO', 'Hola', PRIMEIRO_NOME),
	   HASH_CLIENTE,
	   EMAIL,
	   1,
	   @PAIS,
	   GETDATE() 
  FROM (	
			SELECT *, ROW_NUMBER() OVER (PARTITION BY HASH_CLIENTE ORDER BY HASH_CLIENTE DESC) AS DEDUP
			 FROM #OPERADOR AS A 
			WHERE NULLIF(EMAIL,'') IS NOT NULL
	   ) AS X
 WHERE X.DEDUP = 1
   AND NOT EXISTS (
					  SELECT * 
					    FROM [AMAZON_176].[unilever_crm].[dbo].[TB_PSQ_CLIENTE] AS B
					   WHERE X.HASH_CLIENTE COLLATE SQL_Latin1_General_CP1_CI_AS = B.[HASH]
				  )